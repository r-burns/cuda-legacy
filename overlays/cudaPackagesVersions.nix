final: prev: {
  # Top-level fix-point used in `cudaPackages`' internals
  _cuda = prev._cuda.extend (
    finalCuda: prevCuda: {
      # TODO: Ideally we would add manifests but avoid replacing ones which are already present (e.g., from upstream).
      manifests = import ../pkgs/development/cuda-modules/_cuda/manifests { inherit (final) lib; };

      bootstrapData = final.lib.recursiveUpdate prevCuda.bootstrapData {
        # TODO: Temporary fixes while investigating default versions of redistributables.
        cudaCapabilityToInfo = {
          # cuDNN 9.12 removed support, and we use at least that for CUDA 12
          "6.0".dontDefaultAfterCudaMajorMinorVersion = "11.8";
          "6.1".dontDefaultAfterCudaMajorMinorVersion = "11.8";
          "7.0".dontDefaultAfterCudaMajorMinorVersion = "11.8";
        };

        # NOTE: 25.11 lags behind release, so we re-introduce updates made upstream but unable to be backported.
        nvccCompatibilities = {
          # 13.0 to 13.1 adds support for Clang 21
          # https://docs.nvidia.com/cuda/archive/13.1.1/cuda-installation-guide-linux/index.html#host-compiler-support-policy
          "13.1" = {
            clang = {
              maxMajorVersion = "21";
              minMajorVersion = "7";
            };
            gcc = {
              maxMajorVersion = "15";
              minMajorVersion = "6";
            };
          };

          # No changes from 13.1 to 13.2
          # https://docs.nvidia.com/cuda/cuda-installation-guide-linux/index.html#host-compiler-support-policy
          "13.2" = {
            clang = {
              maxMajorVersion = "21";
              minMajorVersion = "7";
            };
            gcc = {
              maxMajorVersion = "15";
              minMajorVersion = "6";
            };
          };
        };
      };

      # The package sets are unchanged except for the expressions we keep in-tree.
      # If we need to replace a package expression, add an extension to _cuda.extensions and callPackage the
      # replacement.
      extensions = prevCuda.extensions ++ [
        (finalCudaPackages: prevCudaPackages: {
          # cuda_cccl = finalCudaPackages.callPackage ../pkgs/development/cuda-modules/packages/cuda_cccl.nix { };
          libcal =
            finalCudaPackages.callPackage ../pkgs/development/cuda-modules/packages/libcal/package.nix
              { };
          libcublasmp =
            finalCudaPackages.callPackage ../pkgs/development/cuda-modules/packages/libcublasmp.nix
              { };
          libcudss = finalCudaPackages.callPackage ../pkgs/development/cuda-modules/packages/libcudss.nix { };
        })
      ];
    }
  );

  cudaPackagesVersions =
    let
      mkCudaPackages =
        manifestVersions:
        final.callPackage final._cuda.bootstrapData.cudaPackagesPath {
          manifests = final._cuda.lib.selectManifests manifestVersions;
        };

      # NOTE: Thor is supported from CUDA 13.0, so our check needs to capture whether pre-Thor devices were selected.
      hasPreThorJetsonCudaCapability = final.lib.any (final.lib.flip final.lib.versionOlder "10.1");
    in
    {
      cudaPackages_11_4 = mkCudaPackages {
        cublasmp = "0.2.1";
        cuda = "11.4.4";
        cudnn = "9.10.2";
      };

      cudaPackages_11_5 = mkCudaPackages {
        cublasmp = "0.2.1";
        cuda = "11.5.2";
        cudnn = "9.10.2";
      };

      cudaPackages_11_6 = mkCudaPackages {
        cublasmp = "0.2.1";
        cuda = "11.6.2";
        cudnn = "9.10.2";
      };

      cudaPackages_11_7 = mkCudaPackages {
        cublasmp = "0.2.1";
        cuda = "11.7.1";
        cudnn = "9.10.2";
      };

      cudaPackages_11_8 = mkCudaPackages {
        cublasmp = "0.2.1";
        cuda = "11.8.0";
        cudnn = "9.10.2";
      };

      cudaPackages_12_0 = mkCudaPackages {
        cublasmp = "0.8.0";
        cuda = "12.0.1";
        cudnn = "9.20.0";
        cudss = "0.7.1";
      };

      cudaPackages_12_1 = mkCudaPackages {
        cublasmp = "0.8.0";
        cuda = "12.1.1";
        cudnn = "9.20.0";
        cudss = "0.7.1";
      };

      cudaPackages_12_2 = mkCudaPackages {
        cublasmp = "0.8.0";
        cuda = "12.2.2";
        cudnn = "9.20.0";
        cudss = "0.7.1";
      };

      cudaPackages_12_3 = mkCudaPackages {
        cublasmp = "0.8.0";
        cuda = "12.3.2";
        cudnn = "9.20.0";
        cudss = "0.7.1";
      };

      cudaPackages_12_4 = mkCudaPackages {
        cublasmp = "0.8.0";
        cuda = "12.4.1";
        cudnn = "9.20.0";
        cudss = "0.7.1";
      };

      cudaPackages_12_5 = mkCudaPackages {
        cublasmp = "0.8.0";
        cuda = "12.5.1";
        cudnn = "9.20.0";
        cudss = "0.7.1";
      };

      cudaPackages_12_6 =
        let
          inherit (final.cudaPackagesVersions.cudaPackages_12_6.backendStdenv)
            hasJetsonCudaCapability
            hostPlatform
            ;
        in
        mkCudaPackages {
          cublasmp = "0.8.0";
          cuda = "12.6.3";
          cudnn = "9.20.0";
          cudss = "0.7.1";
          cuquantum = "25.09.0";
          cusolvermp = "0.7.0";
          cusparselt = "0.6.3";
          cutensor = "2.3.1";
          nppplus = "0.10.0";
          nvcomp = "5.0.0.6";
          nvjpeg2000 = "0.9.0";
          nvpl = "25.5";
          nvtiff = "0.5.1";
          tensorrt =
            if hasJetsonCudaCapability then
              "10.7.0"
            else if hostPlatform.isAarch64 then
              "10.13.0"
            else
              "10.14.1";
        };

      cudaPackages_12_8 =
        let
          inherit (final.cudaPackagesVersions.cudaPackages_12_8.backendStdenv)
            hasJetsonCudaCapability
            hostPlatform
            ;
        in
        mkCudaPackages {
          cublasmp = "0.8.0";
          cuda = "12.8.1";
          cudnn = "9.20.0";
          cudss = "0.7.1";
          cuquantum = "25.09.0";
          cusolvermp = "0.7.0";
          cusparselt = "0.8.1";
          cutensor = "2.3.1";
          nppplus = "0.10.0";
          nvcomp = "5.0.0.6";
          nvjpeg2000 = "0.9.0";
          nvpl = "25.5";
          nvtiff = "0.5.1";
          tensorrt =
            if hasJetsonCudaCapability then
              "10.7.0"
            else if hostPlatform.isAarch64 then
              "10.13.0"
            else
              "10.14.1";
        };

      cudaPackages_12_9 =
        let
          inherit (final.cudaPackagesVersions.cudaPackages_12_9.backendStdenv)
            hasJetsonCudaCapability
            hostPlatform
            ;
        in
        mkCudaPackages {
          cublasmp = "0.8.0";
          cuda = "12.9.1";
          cudnn = "9.20.0";
          cudss = "0.7.1";
          cuquantum = "25.09.0";
          cusolvermp = "0.7.0";
          cusparselt = "0.8.1";
          cutensor = "2.3.1";
          nppplus = "0.10.0";
          nvcomp = "5.0.0.6";
          nvjpeg2000 = "0.9.0";
          nvpl = "25.5";
          nvtiff = "0.5.1";
          tensorrt =
            if hasJetsonCudaCapability then
              "10.7.0"
            else if hostPlatform.isAarch64 then
              "10.13.0"
            else
              "10.14.1";
        };

      cudaPackages_13_0 =
        let
          inherit (final.cudaPackagesVersions.cudaPackages_13_0.backendStdenv)
            requestedJetsonCudaCapabilities
            ;
        in
        mkCudaPackages {
          cublasmp = "0.8.0";
          cuda = "13.0.2";
          cudnn = "9.20.0";
          cudss = "0.7.1";
          cuquantum = "25.09.0";
          cusolvermp = "0.7.0";
          cusparselt = "0.8.1";
          cutensor = "2.3.1";
          nppplus = "0.10.0";
          nvcomp = "5.0.0.6";
          nvjpeg2000 = "0.9.0";
          nvpl = "25.5";
          nvtiff = "0.5.1";
          tensorrt =
            if hasPreThorJetsonCudaCapability requestedJetsonCudaCapabilities then "10.7.0" else "10.14.1";
        };

      cudaPackages_13_1 =
        let
          inherit (final.cudaPackagesVersions.cudaPackages_13_1.backendStdenv)
            requestedJetsonCudaCapabilities
            ;
        in
        mkCudaPackages {
          cublasmp = "0.8.0";
          cuda = "13.1.1";
          cudnn = "9.20.0";
          cudss = "0.7.1";
          cuquantum = "25.09.0";
          cusolvermp = "0.7.0";
          cusparselt = "0.8.1";
          cutensor = "2.3.1";
          nppplus = "0.10.0";
          nvcomp = "5.0.0.6";
          nvjpeg2000 = "0.9.0";
          nvpl = "25.5";
          nvtiff = "0.5.1";
          tensorrt =
            if hasPreThorJetsonCudaCapability requestedJetsonCudaCapabilities then "10.7.0" else "10.14.1";
        };

      cudaPackages_13_2 =
        let
          inherit (final.cudaPackagesVersions.cudaPackages_13_2.backendStdenv)
            requestedJetsonCudaCapabilities
            ;
        in
        mkCudaPackages {
          cublasmp = "0.8.0";
          cuda = "13.2.0";
          cudnn = "9.20.0";
          cudss = "0.7.1";
          cuquantum = "25.09.0";
          cusolvermp = "0.7.0";
          cusparselt = "0.8.1";
          cutensor = "2.3.1";
          nppplus = "0.10.0";
          nvcomp = "5.0.0.6";
          nvjpeg2000 = "0.9.0";
          nvpl = "25.5";
          nvtiff = "0.5.1";
          tensorrt =
            if hasPreThorJetsonCudaCapability requestedJetsonCudaCapabilities then "10.7.0" else "10.14.1";
        };
    };
}
